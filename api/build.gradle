plugins {
    id 'java'
    id 'org.springframework.boot' version '2.5.0'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'org.graalvm.buildtools.native' version '0.9.20'

    id 'org.sonarqube' version '4.0.0.2929'
    id 'jacoco'
}

group = 'edu.upc.essi.dtim'
version = 'unspecified'

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/milestone' }
    maven { url 'https://repo.spring.io/snapshot' }
    maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots/' }
}

configurations {
    all {
        // Do not exclude the following dependency: exclude group: 'org.apache.logging.log4j', module: 'log4j-to-slf4j'
        exclude group: 'ch.qos.logback', module: 'logback-classic'
        exclude group: 'org.apache.logging.log4j', module: 'log4j-to-slf4j'
        exclude group: 'org.slf4j', module: 'slf4j-reload4j'
    }
}

dependencies {
    implementation project(':Modules:NextiaCore')
    implementation project(':Modules:NextiaDataLayer')
    implementation project(':Modules:NextiaBS')
    implementation project(':Modules:NextiaDI')
    implementation project(':Modules:NextiaJD')
    implementation project(':Modules:NextiaQR')

    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    // ORM Dependencies
    implementation group: 'org.hibernate.orm', name: 'hibernate-core', version: '6.0.0.Final'
    implementation 'org.eclipse.persistence:javax.persistence:2.0.0'
    implementation group: 'org.eclipse.persistence', name: 'org.eclipse.persistence.jpa', version: '2.5.0'
    runtimeOnly group: 'com.h2database', name: 'h2', version: '2.1.214'

    // JENA Dependencies
    implementation group: 'org.apache.jena', name: 'apache-jena-libs', version: '4.1.0', ext: 'pom'
    implementation group: 'org.apache.jena', name: 'jena-querybuilder', version: '4.1.0'

    // Other dependencies
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.9'
    implementation group: 'io.delta', name: 'delta-core_2.13', version: '2.3.0'
    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.14'
    implementation group: 'org.apache.httpcomponents', name: 'httpcore', version: '4.4.16'
    implementation group: 'org.apache.spark', name: 'spark-sql_2.13', version: '3.3.2'
    implementation group: 'com.opencsv', name: 'opencsv', version: '5.9'
    implementation('org.codehaus.janino:commons-compiler'){
        version {
            strictly '3.0.16'
        }
    }
    implementation('org.codehaus.janino:janino'){
        version {
            strictly '3.0.16'
        }
    }
}

jacocoTestReport {
    reports {
        xml.required = true
        csv.required = true
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }
    dependsOn test
}

task generateDocs(type: Javadoc) {
    source = sourceSets.main.allJava
    destinationDir = file("${buildDir}/docs")
    options.links("https://docs.oracle.com/javase/8/docs/api/")
    failOnError false
}

sonarqube {
    properties {
        property "sonar.projectKey", "dtim-upc_newODIN"
        property "sonar.organization", "dtim"
        property "sonar.host.url", "https://sonarcloud.io"
        property 'sonar.core.codeCoveragePlugin', 'jacoco'
        property "sonar.exclusions", "/src/main/java/edu/upc/essi/dtim/odin/NextiaGraphy/**"
    }
}
tasks['sonarqube'].dependsOn test

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

task bootRun(type: JavaExec) {
    mainClass = 'edu.upc.essi.dtim.odin.OdinApplication'
    classpath = sourceSets.main.runtimeClasspath
    jvmArgs '--add-exports', 'java.base/sun.nio.ch=ALL-UNNAMED'
}
bootJar  {
    manifest {
        attributes (
                'Main-Class': 'edu.upc.essi.dtim.odin.OdinApplication'
        )
    }
}

test {
    useJUnitPlatform()
}